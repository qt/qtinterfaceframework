# Custom compiler for qface:
#
# Example input:
#   QFACE_SOURCES += my.foo.qface [required]
#   QFACE_MODULE_NAME = my_module_name [required]
#   QFACE_FORMAT = frontend (or backend, simulator... -- defaults to "frontend")
#   QFACE_OUTPUT_DIR = my_output_dir (defaults to current build dir)
#

isEmpty(QFACE_FORMAT): QFACE_FORMAT = frontend
isEmpty(QFACE_OUTPUT_DIR): QFACE_OUTPUT_DIR = $$OUT_PWD
isEmpty(QFACE_SOURCES): error("QFACE_SOURCES must be set!")
isEmpty(QFACE_MODULE_NAME): error("QFACE_MODULE_NAME must be set!")

qface_sources.output = $$QFACE_OUTPUT_DIR/${QMAKE_FILE_BASE}/$${QFACE_MODULE_NAME}module.cpp
qface_sources.commands = $${PWD}/generate.py -f $$QFACE_FORMAT ${QMAKE_FILE_NAME} $$QFACE_OUTPUT_DIR
qface_sources.input = QFACE_SOURCES
qface_sources.CONFIG += no_link
QMAKE_EXTRA_COMPILERS += qface_sources
